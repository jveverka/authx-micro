plugins {
    id 'org.springframework.boot' version '2.7.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

group = 'one.microproject'
version = "${authXVersion}"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.bouncycastle:bcpg-jdk15on:1.70'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.70'
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.9'
    implementation 'net.logstash.logback:logstash-logback-encoder:7.2'
    implementation 'org.codehaus.janino:janino:3.1.7'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:junit-jupiter:1.17.2'
    testImplementation 'org.testcontainers:mongodb:1.17.2'
    testImplementation 'com.squareup.okhttp3:okhttp:4.9.3'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println("----------------------------------------------------------------------------------------")
            println("${result.resultType} " +
                    "(${result.testCount} tests, " +
                    "${result.successfulTestCount} successes, " +
                    "${result.failedTestCount} failures, " +
                    "${result.skippedTestCount} skipped)")
            println("----------------------------------------------------------------------------------------")
        }
    }
}

jar {
    //do not create build/libs/authx-micro-service-*-plain.jar file
    enabled = false
}
